@doc raw"""
    ScalableGP(풮, 洧니; solver=:celerite)

Scalable Gaussian Process for semi-separable covariance functions.

- `풮` : mean or mean function of the Gaussian Process
- `洧니` : covariance function
- `solver` : indicates which solver to use for the likelihood computation.

A scalable Gaussian process has a covariance function formed of semi-separable kernels

# Example
```julia
using Pioran
洧 = SingleBendingPowerLaw(1.0, 1.0, 2.0)
洧니 = approx(洧, 1e-4, 1e-1, 30, 2.31,basis_function="SHO")
풮 = 1.2

f = ScalableGP(洧니) # zero-mean GP
f = ScalableGP(풮, 洧니) # with mean 풮
```
See [Foreman-Mackey et al. (2017)](https://ui.adsabs.harvard.edu/abs/2017AJ....154..220F) for more details.
"""
struct ScalableGP{Typef <: GP{<:AbstractGPs.MeanFunction}, Tk <: SemiSeparable, Tsolv <: Symbol} <: AbstractGPs.AbstractGP
    f::Typef
    kernel::Tk
    solver::Tsolv
end

# const FinitePosteriorGP = AbstractGPs.FiniteGP{<:ScalableGP}

# ScalableGP(f::GP) = ScalableGP(f, f.kernel)
ScalableGP(f::GP) = ScalableGP(f, f.kernel, :celerite)

ScalableGP(kernel::SemiSeparable) = ScalableGP(GP(0.0, kernel), kernel, :celerite)
ScalableGP(mean::Real, kernel::SemiSeparable) = ScalableGP(GP(mean, kernel), kernel, :celerite)

ScalableGP(mean::Real, kernel::SemiSeparable, solver::Symbol) = ScalableGP(GP(mean, kernel), kernel, solver)

ScalableGP(mean::AbstractGPs.MeanFunction, kernel::SemiSeparable) = ScalableGP(GP(mean, kernel), kernel, :celerite)

const FiniteScalableGP = AbstractGPs.FiniteGP{<:ScalableGP}

struct PosteriorGP{Typef <: FiniteScalableGP, Ty <: AbstractVecOrMat{<:Real}} <: AbstractGPs.AbstractGP
    f::Typef
    y::Ty
end

"""
    posterior(f::ScalableGP, y::AbstractVecOrMat{<:Real})

Compute the posterior Gaussian process `fp` given the GP `f` and the data `y`.
"""
posterior(f::FiniteScalableGP, y::AbstractVecOrMat{<:Real}) = PosteriorGP(f, y)

"""
    _predict_mean(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real})

    Compute the Posterior mean of the GP at the points 픣.
"""
function _predict_mean(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real})
    K = fp.f.f.kernel
    x = fp.f.x
    y = fp.y .- mean_vector(fp.f.f.f.mean, x)
    픢2 = diag(fp.f.풖y)
    return predict(K, 픣, x, y, 픢2) .+ mean_vector(fp.f.f.f.mean, 픣)
end

"""
    _predict_cov(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real})
    Compute the posterior covariance of the GP at the points 픣.
"""
function _predict_cov(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real})
    K = fp.f.f.kernel
    x = fp.f.x
    픢2 = diag(fp.f.풖y)
    return predict_cov(K, 픣, x, 픢2)
end

"""
    mean(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real})
    mean(fp::PosteriorGP)

Compute the mean of the posterior GP at the points 픣.

"""
AbstractGPs.mean(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real}) = _predict_mean(fp, 픣)
AbstractGPs.mean(fp::PosteriorGP) = _predict_mean(fp, fp.f.x)
"""
    cov(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real})
    cov(fp::PosteriorGP)

Compute the covariance of the posterior GP at the points 픣.
"""
AbstractGPs.cov(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real}) = _predict_cov(fp, 픣)
AbstractGPs.cov(fp::PosteriorGP) = _predict_cov(fp, fp.f.x)
"""
    std(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real})
    std(fp::PosteriorGP)

Compute the standard deviation of the posterior GP at the points 픣.
"""
AbstractGPs.std(fp::PosteriorGP) = sqrt.(diag(_predict_cov(fp, fp.f.x)))
AbstractGPs.std(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real}) = sqrt.(diag(_predict_cov(fp, 픣)))

function AbstractGPs.rand(rng::AbstractRNG, fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real}, N::Int64 = 1)
    풮 = mean(fp, 픣)
    풖 = cov(fp, 픣)
    post_dist = MvNormal(풮, 풖)

    return rand(rng, post_dist, N)
end

"""
    rand(rng::AbstractRNG, fp::PosteriorGP, N::Int=1)
    rand(rng::AbstractRNG, fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real}, N::Int=1)
    rand(fp::PosteriorGP, N::Int=1)

Sample `N` realisations from the posterior GP `fp` at the points `픣`.
"""
function AbstractGPs.rand(rng::AbstractRNG, fp::PosteriorGP, N::Int64 = 1)
    풮 = mean(fp)
    풖 = cov(fp)
    post_dist = MvNormal(풮, 풖)

    return rand(rng, post_dist, N)
end

AbstractGPs.rand(fp::PosteriorGP, N::Int64) = AbstractGPs.rand(Random.GLOBAL_RNG, fp, N)
AbstractGPs.rand(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real}, N::Int64) = AbstractGPs.rand(Random.GLOBAL_RNG, fp, 픣, N)
AbstractGPs.rand(fp::PosteriorGP, 픣::AbstractVecOrMat{<:Real}) = AbstractGPs.rand(Random.GLOBAL_RNG, fp, 픣, 1)


function randScalableGP(rng::AbstractRNG, f::FiniteScalableGP)
    픢2 = diag(f.풖y)
    return simulate(rng, f.f.kernel, f.x, 픢2) + mean_vector(f.f.f.mean, f.x)
end

function randScalableGP(rng::AbstractRNG, f::FiniteScalableGP, t::AbstractVecOrMat{<:Real})
    픢2 = zeros(length(t))
    return simulate(rng, f.f.kernel, t, 픢2) + mean_vector(f.f.f.mean, t)
end

"""
    rand(rng::AbstractRNG, f::ScalableGP)
    rand(rng::AbstractRNG, f::ScalableGP, t::AbstractVecOrMat{<:Real})
    rand(f::ScalableGP)
    rand(f::ScalableGP, t::AbstractVecOrMat{<:Real})

Draw a realisation from the GP `f` at the points `t`.
"""
AbstractGPs.rand(rng::AbstractRNG, f::FiniteScalableGP) = randScalableGP(rng, f)
AbstractGPs.rand(rng::AbstractRNG, f::FiniteScalableGP, t::AbstractVecOrMat{<:Real}) = randScalableGP(rng, f, t)
AbstractGPs.rand(f::FiniteScalableGP) = randScalableGP(Random.GLOBAL_RNG, f)
AbstractGPs.rand(f::FiniteScalableGP, t::AbstractVecOrMat{<:Real}) = randScalableGP(Random.GLOBAL_RNG, f, t)

"""
    logpdf(f::ScalableGP, Y::AbstractVecOrMat{<:Real})

Compute the log-likelihood of the data Y given the GP f.
"""
function Distributions.logpdf(f::FiniteScalableGP, Y::AbstractVecOrMat{<:Real})
    픢2 = diag(f.풖y)
    y = Y .- mean_vector(f.f.f.mean, f.x)
    return log_likelihood(f.f.kernel, f.x, y, 픢2; solver = f.f.solver)
end
